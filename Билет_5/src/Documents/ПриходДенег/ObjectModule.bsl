Процедура ОбработкаПроведения(Отказ, Режим)

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВзаиморасчетыОстатки.Проект,
	|	ВзаиморасчетыОстатки.СуммаРубОстаток,
	|	ВзаиморасчетыОстатки.Проект.Валюта КАК Валюта
	|ПОМЕСТИТЬ ВременнаяТаблица1
	|ИЗ
	|	РегистрНакопления.Взаиморасчеты.Остатки(&Дата, Контрагент = &Контрагент) КАК ВзаиморасчетыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица1.Проект,
	|	-ВременнаяТаблица1.СуммаРубОстаток как СуммаРубОстаток,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс
	|ИЗ
	|	ВременнаяТаблица1 КАК ВременнаяТаблица1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта В
	|			(ВЫБРАТЬ
	|				ВременнаяТаблица1.Валюта
	|			ИЗ
	|				ВременнаяТаблица1 КАК ВременнаяТаблица1)) КАК КурсыВалютСрезПоследних
	|		ПО ВременнаяТаблица1.Валюта = КурсыВалютСрезПоследних.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВременнаяТаблица1.Проект.ДатаОплаты";

	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Дата", Дата);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Осталось = СуммаПлатежа;
	Пока Осталось > 0 И Выборка.Следующий() Цикл
		Движение = Движения.Взаиморасчеты.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Выборка.Проект;
		Движение.СуммаРуб = Мин(Осталось, Выборка.СуммаРубОстаток);
		Движение.СуммаВал = Движение.СуммаРуб / Выборка.Курс;
		Осталось=Осталось - Движение.СуммаРуб;
	КонецЦикла;
	Движения.Взаиморасчеты.Записывать = Истина;

	Если Осталось > 0 Тогда
		Движение = Движения.Взаиморасчеты.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Контрагент = Контрагент;
		Движение.СуммаРуб = Осталось;
	КонецЕсли;

КонецПроцедуры